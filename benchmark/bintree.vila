lớp Tree
{
    khởi_tạo(vật, độ_sâu)
    {
        đây.vật = vật;
        đây.độ_sâu = độ_sâu;

        nếu (độ_sâu > 0)
        {
            biến vật2 = vật + vật;
            độ_sâu = độ_sâu - 1;
            đây.trái = Tree(vật2 - 1, độ_sâu);
            đây.phải = Tree(vật2, độ_sâu);
        }
        thì
        {
            đây.trái = trống;
            đây.phải = trống;
        }
    }

    check()
    {
        nếu (đây.trái == trống)
        {
            trả đây.vật;
        }

        trả đây.vật + đây.trái.check() - đây.phải.check();
    }
}

biến độ_sâu_min = 4;
biến độ_sâu_max = 12;
biến độ_sâu_stretch = độ_sâu_max + 1;

biến start = đồng_hồ();

in("Cây stretch với độ sâu: " + độ_sâu_stretch);
in("Check: " + Tree(0, độ_sâu_stretch).check());

biến cây_lâu_dài = Tree(0, độ_sâu_max);

// số_lần = 2 ** độ_sâu_max
biến số_lần = 1;
biến s = 0;

khi (s < độ_sâu_max)
{
    số_lần = số_lần * 2;
    s = s + 1;
}

biến độ_sâu = độ_sâu_min;

khi (độ_sâu < độ_sâu_stretch)
{
    biến check = 0;
    biến i = 1;

    khi (i <= số_lần)
    {
        check = check + Tree(i, độ_sâu).check() + Tree(-i, độ_sâu).check();
        i += 1;
    }

    in("Số cây: " + số_lần * 2);
    in("Độ sâu: " + độ_sâu);
    in("Check: " + check);

    số_lần = số_lần / 4;
    độ_sâu = độ_sâu + 2;
}

in("");
in("Cây với độ sâu: " + độ_sâu_max);
in("Check: " + cây_lâu_dài.check());
in("Elapsed: " + (đồng_hồ() - start));
