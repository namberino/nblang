//------------------------------------//
// Bản quyền © 2024 Nguyễn Bình Nam
// Được cấp phép theo Giấy phép Apache v2.0
//------------------------------------//

biến PI = 3.1415926535897932384626433832795028841971;

hàm factorial(n)
{
    nếu (n == 0 hoặc n == 1)
    {
        trả 1;
    }

    trả n * factorial(n - 1);
}

hàm sin(x)
{
    biến result = 0;

    với (biến i = 0; i < 20; i += 1)
    {
        biến sign = -1 ** i;
        biến term = (x ** (2 * i + 1)) / factorial(2 * i + 1);
        result += sign * term;
    }

    trả result;
}

hàm cos(x)
{
    biến result = 0;

    với (biến i = 0; i < 20; i += 1)
    {
        biến sign = -1 ** i;
        biến term = (x ** (2 * i)) / factorial(2 * i);
        result += sign * term;
    }

    trả result;
}

hàm tan(x)
{
    trả sin(x) / cos(x);
}

hàm cot(x)
{
    trả cos(x) / sin(x);
}

hàm exp(x)
{
    biến result = 1;
    biến term = 1;

    với (biến i = 1; i < 20; i += 1)
    {
        term = term * x / i;
        result = result + term;
    }

    trả result;
}

hàm ln(x)
{
    nếu (x <= 0)
    {
        trả 0 / 0; // error
    }

    biến y = (x - 1) / (x + 1);
    biến y2 = y * y;
    biến result = 0;
    biến term = y;

    với (biến i = 1; i < 100; i += 2)
    {
        result = result + term / i;
        term = term * y2;
    }

    trả 2 * result;
}

hàm log10(x)
{
    trả ln(x) / ln(10);
}

hàm log2(x)
{
    trả ln(x) / ln(2);
}

hàm log(x, b)
{
    trả ln(x) / ln(b);
}

hàm sqrt(x)
{
    nếu (x < 0)
    {
        trả 0 / 0; // error
    }

    biến guess = x;
    biến epsilon = 0.000001;

    khi ((guess * guess - x) > epsilon)
    {
        guess = (guess + x / guess) / 2;
    }

    trả guess;
}
